{ stdenv
, autoreconfHook
, fetchurl
, python3Packages

, kerberos
, keyutils
, libcap-ng
, pam
, samba_client
, talloc
}:

stdenv.mkDerivation rec {
  name = "cifs-utils-6.8";

  src = fetchurl {
    url = "mirror://samba/linux-cifs/cifs-utils/${name}.tar.bz2";
    hashOutput = false;
    sha256 = "e7d1f6050c43f21f82cd77e288eb756755effd22f0c310fc2c525df9d41dff79";
  };

  # 6.8 was generated by upstream incorrectly
  # libcap-ng's aclocal was not present and generated a broken configure
  nativeBuildInputs = [
    autoreconfHook
    python3Packages.docutils
  ];

  buildInputs = [
    kerberos
    keyutils
    libcap-ng
    pam
    samba_client
    talloc
  ];

  postPatch = ''
    sed -i 's#, rst2man,#, rst2man.py,#' configure.ac
    sed -i 's,rst2man,rst2man.py,g' Makefile.am
  '';

  configureFlags = [
    "--sysconfdir=/etc"
    "--localstatedir=/var"
  ];

  preBuild = ''
    makeFlagsArray+=("root_sbindir=$out/sbin")
  '';

  passthru = {
    srcVerification = fetchurl {
      failEarly = true;
      pgpsigUrls = map (n: "${n}.asc") src.urls;
      pgpKeyFingerprint = "D869 2123 C406 5DEA 5E0F  3AB5 249B 39D2 4F25 E3B6";
      inherit (src) urls outputHash outputHashAlgo;
    };
  };

  meta = with stdenv.lib; {
    description = "Tools for managing Linux CIFS client filesystems";
    homepage = http://www.samba.org/linux-cifs/cifs-utils/;
    license = licenses.lgpl3;
    maintainers = with maintainers; [
      wkennington
    ];
    platforms = with platforms;
      x86_64-linux;
  };
}
